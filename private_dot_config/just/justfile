set shell := ["zsh", "-uc"]
set windows-shell := ["pwsh.exe", "-NoLogo", "-Command"]

templates_dir := env_var("XDG_DATA_HOME") / "templates"

[private]
@default:
    just --list --justfile {{justfile()}}

# Create a new justfile in the current directory
[no-cd, group('just')]
init:
    cp {{templates_dir}}/justfile justfile

# Edit the global justfile
[no-cd, group('just')]
edit:
    chezmoi edit --apply {{justfile_directory()}}/justfile

# Create a new .editorconfig in the current directory
[no-cd]
editorconfig:
    cp {{templates_dir}}/.editorconfig .editorconfig

# Start the email AutoHotkey script
[no-cd, windows, group('autohotkey')]
ah-start:
    Invoke-Item {{home_directory()}}/email.ahk

# Stop the email AutoHotkey script
[no-cd, windows, group('autohotkey')]
ah-stop:
    Stop-Process -Name:AutoHotkey64 -ErrorAction Ignore

# Start Apex Legends
[no-cd, windows, group('steam')]
apex-start: wm-stop ah-stop
    explorer 'steam://rungameid/1172470'

# Restart wm and ah after game closed
[no-cd, windows, group('steam')]
apex-stop: ah-start wm-start

# Start komorebi and whkd
[no-cd, windows, group('komorebi')]
wm-start:
    komorebic start --whkd --bar

# Stop komorebi
[no-cd, windows, group('komorebi')]
wm-stop:
    komorebic stop --whkd --bar

# Restart komorebi
[no-cd, windows, group('komorebi')]
wm-restart: wm-stop wm-start

# Check for winget updates
[no-cd, no-exit-message, windows, group('winget')]
up-check:
    winget upgrade --include-unknown --source winget

# Install all winget updates
[no-cd, no-exit-message, windows, group('winget')]
up-all:
    winget upgrade --all --source winget --accept-package-agreements

# Install winget package by exact id
[no-cd, no-exit-message, windows, group('winget')]
up-install *args:
    winget upgrade {{args}}

# Check for pip updates
[no-cd, group('python')]
pip-outdated:
    python -m pip list --outdated

# Update a pip package
[no-cd, group('python')]
pip-upgrade *packages:
    python -m pip install --upgrade {{packages}}
