set shell := ["zsh", "-uc"]
set windows-shell := ["pwsh.exe", "-NoLogo", "-Command"]

[private]
@default:
    just --list --justfile {{justfile()}}

# Create a new justfile in the current directory
[no-cd, group('just')]
init:
    cp {{justfile_directory()}}/init_template.just justfile

# Edit the global justfile
[no-cd, group('just')]
edit:
    chezmoi edit --apply {{justfile_directory()}}/justfile

# Start komorebi and whkd
[no-cd, windows, group('komorebi')]
wm-start:
    komorebic start --whkd

# Stop komorebi
[no-cd, windows, group('komorebi')]
wm-stop:
    komorebic stop

# Check for winget updates
[no-cd, no-exit-message, windows, group('winget')]
up-check:
    winget upgrade --include-unknown --source winget

# Install winget updates
[no-cd, no-exit-message, windows, group('winget')]
up-install:
    winget upgrade --all --source winget --accept-package-agreements
