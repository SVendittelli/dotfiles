set shell := ["zsh", "-uc"]
set windows-shell := ["pwsh.exe", "-NoLogo", "-Command"]

[private]
@default:
    just --list --justfile {{justfile()}}

# Create a new justfile in the current directory
[no-cd, group('just')]
init:
    cp {{justfile_directory()}}/init_template.just justfile

# Edit the global justfile
[no-cd, group('just')]
edit:
    chezmoi edit --apply {{justfile_directory()}}/justfile

# Create a new .editorconfig in the current directory
[no-cd]
editorconfig:
    cp {{justfile_directory()}}/template.editorconfig .editorconfig

# Start komorebi and whkd
[no-cd, windows, group('komorebi')]
wm-start:
    komorebic start --whkd --bar

# Stop komorebi
[no-cd, windows, group('komorebi')]
wm-stop:
    komorebic stop --whkd --bar

# Restart komorebi
[no-cd, windows, group('komorebi')]
wm-restart: wm-stop wm-start

# Check for winget updates
[no-cd, no-exit-message, windows, group('winget')]
up-check:
    winget upgrade --include-unknown --source winget

# Install all winget updates
[no-cd, no-exit-message, windows, group('winget')]
up-all:
    winget upgrade --all --source winget --accept-package-agreements

# Install winget package by exact id
[no-cd, no-exit-message, windows, group('winget')]
up-install *args:
    winget upgrade {{args}}
